buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath "com.netflix.spinnaker.gradle:spinnaker-gradle-project:3.1.0"
    classpath "com.moowork.gradle:gradle-node-plugin:0.8"
    classpath "be.filipblondeel.gradle:gradle-gulp-plugin:0.1"
  }
}

publishing {
  publications {
    deb(MavenPublication) {
      artifact buildDeb
    }
  }
}

group = "com.netflix.spinnaker.deck"

apply plugin: "spinnaker.project"
apply plugin: "nebula.ospackage"
apply plugin: "com.moowork.node"
apply plugin: "java"

def netflixAccount = "prestaging"
if (System.getenv("NETFLIX_ACCOUNT")) {
  netflixAccount = System.getenv("NETFLIX_ACCOUNT")
}

npm_run_build {
  environment = [
    "GATE_HOST": "spinnaker-api.${ netflixAccount }.netflix.net",
    "AUTH_ENDPOINT": "https://spinnaker-api.${ netflixAccount }.netflix.net/auth/info",
    "PROVIDERS": file("./deployable/providers/${ netflixAccount }.json").getText(),
  ]
}

jar {
  dependsOn(npm_run_build)
  from(fileTree("build/webpack")) {
    into "META-INF/resources"
  }
}

node {
    version = "0.12.3"
    npmVersion = "2.8.3"
    download = true
}

task copyToWebRoot(type: Copy) {
  from "build/webpack"
  into "/var/www"
}

npm_run_build.dependsOn "npmInstall"
copyToWebRoot.dependsOn npm_run_build
npm_test.dependsOn npm_run_build
test.dependsOn npm_test

String toVers(String v) {
  int idx = v.indexOf("-")
  if (idx != -1) {
    return v.substring(0, idx)
  }
  return v
}

ospackage {
  packageName = "spinnaker-deck-new-deployable"
  version = toVers(project.version.toString())
  release "3"
  into "/apps/deck"
  requires "apache2"
  from "."
  requires "prana-release"
  postInstall "./gradlew buildStatic"
}
